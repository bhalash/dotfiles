# zmodload zsh/zprofuto

# TODO(mark 2022-01-05): have a read of flowchart to split stuff:
# See: https://blog.flowblok.id.au/2013-02/shell-startup-scripts.html

# ! Zsh: Base {{{

# TODO(mark 2021-12-29): look at setting more options? Some cool stuff in there
# See: https://linux.die.net/man/1/zshoptions

# Permit shorter loop syntax
setopt short_loops
setopt function_arg_zero
# Resolve symlinks
setopt chase_links
# No beeps
unsetopt beep
# Safe rm
unsetopt rm_star_silent
# Enable color output
autoload colors && colors

# }}}

# Zsh: Autoload {{{

fpath=($fpath "${ZSH_DIR}/autoload")

# }}}

# ! Zsh: Plugins {{{

# TODO(mark 2021-12-30): find more plugins? Seems to be some interesting
# options out there, but eh, installing stuff. I'm /very/ happy with my zsh
# startup time right now, without the addition of other crap to slow it

# plugins=(git history-substring-search)
# NOTE(mark 2021-12-30): https://github.com/unixorn/awesome-zsh-plugins

autoload -Uz compinit && compinit -C

# }}}

# Zsh: History {{{

HISTFILE="${HOME}/.zsh_history"
HISTSIZE=2000
SAVEHIST=$HISTSIZE

setopt INC_APPEND_HISTORY
setopt HIST_FIND_NO_DUPS
setopt HIST_IGNORE_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_VERIFY
unsetopt HIST_BEEP

# Bind ctrl + r to history search
bindkey '^R' history-incremental-search-backward

if [[ ! -f $HISTFILE ]]; then
  touch $HISTFILE
fi

# }}}

# Zsh: Vim Mode {{{

bindkey -v

# Decrease lag in vim mode: This reduces time between input keystrokes to 1ms.
# See: https://dougblack.io/words/zsh-vi-mode.html
KEYTIMEOUT=1

zle -N zle-line-init
zle -N zle-keymap-select

# Backward deletion keybinds in insert mode.
bindkey '^?' backward-delete-char
bindkey '^W' backward-kill-word
bindkey '^H' backward-delete-char
bindkey '^U' backward-kill-line

# }}}

# Zsh: Temp {{{

if [[ -z $TMPDIR ]]; then
  export TMPDIR='/var/tmp/'
fi

if [[ ! -d $TMPDIR ]]; then
  mkdir -p -m 700 $TMPDIR
fi

# }}}

# ! Widget: bind cd to <tab> {{{

# TODO(mark 2021-12-29): very much unused by me. Would be nice to have FZF
# completion here

function _tab_cd {
  if [[ $#BUFFER == 0 ]]; then
    BUFFER="cd "
    CURSOR=3
    zle list-choices
  else
    zle expand-or-complete
  fi
}

zle -N _tab_cd
bindkey '^I' _tab_cd

# }}}

# Widget: Foreground/Background Vim {{{

# Stolen from Reddit, this is the Zsh equivalent of:
#
#   stty susp undef
#   bind '"\C-z":"fg\015"'
#
# Set ctrl + z to fg vim. It makes a great, efficient toggle on slow systems.

function _fg_vim {
  fg nvim 2>/dev/null
}

stty susp undef
zle -N _fg_vim
bindkey '^z' _fg_vim

# }}}

# Git {{{

function gr {
  # cd to git project root folder
  if $(git rev-parse 2> /dev/null); then
    builtin cd "$(git rev-parse --show-toplevel)"
  else
    echo "cd: not a git project: ${PWD}"
  fi
}

# }}}

# ! FZF {{{

[[ -f $HOME/.fzf.zsh ]] && source $HOME/.fzf.zsh

# TODO(mark 2021-12-29): this whole section needs revision
# See: https://github.com/junegunn/fzf#environment-variables

# TODO(mark 2021-12-29): needs revision
export FZF_DEFAULT_COMMAND='rg --files --follow --hidden -g "!.git/*" -g "!.gitkeep"'
export FZF_CTRL_T_COMMAND='rg --files --null | xargs -0 dirname | sort | uniq'
export FZF_ALT_C_COMMAND='rg --files --null | xargs -0 dirname | sort | uniq'

# Browse for a file and open it in vim
alias vo='vim $(fzf --preview "batcat --style=numbers --color=always --line-range :500 {}")'

if [[ $OSTYPE =~ 'linux-gnu' ]]; then
  # TODO(mark 2022-01-04): same path on macOS?
  source '/usr/share/doc/fzf/examples/key-bindings.zsh'
  source '/usr/share/doc/fzf/examples/completion.zsh'
fi

# }}}

# NVM {{{

# Lazy load nvm and default node version
# See: https://peterlyons.com/problog/2018/01/zsh-lazy-loading/

function nvm {
  if [[ -d "$HOME/.nvm" ]]; then
    unfunction "$0"
    export NVM_DIR="$HOME/.nvm"
    source "$NVM_DIR/nvm.sh"
    nvm "$@"
  else
    echo "nvm is not installed" >&2
    return 1
  fi
}

# }}}

# Aliases {{{

alias agu='sudo apt-get update && sudo apt-get -y dist-upgrade'
alias batcat='bat'
alias tmux='tmux -2' # "Force tmux to assume the terminal supports 256 colours."
alias vi='nvim'
alias vim='nvim'
alias vimdiff='nvim -d'

hash -d zdir=$ZSH_DIR
hash -d dotfiles=$DOTFILES_DIR

# }}}

# Prompt {{{

source "${ZSH_DIR}/prompt.zsh"

# }}}

# Private Zshrc {{{

if [[ -f "${HOME}/.private.zsh" ]]; then
  source "${HOME}/.private.zsh"
fi

# }}}

# zprof
