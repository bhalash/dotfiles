# TODO(mark 2022-01-05): have a read of flowchart to split stuff:
# See: https://blog.flowblok.id.au/2013-02/shell-startup-scripts.html

# ! Zsh: Base {{{

# TODO(mark 2021-12-29): look at setting more options? Some cool stuff in there
# See: https://linux.die.net/man/1/zshoptions

# Enable startup profiling
# See: https://zsh.sourceforge.io/Doc/Release/Zsh-Modules.html
# zmodload zsh/zprof

# Permit shorter loop syntax
setopt short_loops
setopt function_arg_zero
# Resolve symlinks
setopt chase_links
# cd more shortly
setopt autocd
# No beeps
unsetopt beep
# Safe rm
unsetopt rm_star_silent
# Enable color output
autoload colors && colors

# }}}

# XDG {{{

if [[ ! -v XDG_CONFIG_HOME ]]; then
  export XDG_CONFIG_HOME="$HOME/.config"
fi

if [[ ! -v XDG_DATA_HOME ]]; then
  export XDG_DATA_HOME="$HOME/.local/share"
fi

# }}}

# ZSH: Autoload {{{

fpath=($fpath "${ZSH_DIR}/autoload")

# }}}

# ZSH: History {{{

export HISTFILE="$HOME/.zsh_history"
export SAVEHIST=5000 # history written to file
export HISTSIZE=1000 # history loaded to memory

if [[ ! -f $HISTFILE ]]; then
  touch $HISTFILE
fi

setopt extendedhistory # enable to log timestamp alongside command
setopt append_history inc_append_history # append history for each command, not when shell exits
# setopt share_history # share history fully between concurrent sessions
setopt hist_ignore_all_dups
setopt hist_ignore_space
setopt hist_reduce_blanks
setopt hist_save_no_dups
setopt hist_verify
unsetopt hist_beep

# Bind ctrl + r to history search
bindkey '^R' history-incremental-search-backward

# }}}

# Zsh: Vim Mode {{{

bindkey -v

# Decrease lag in vim mode: This reduces time between input keystrokes to 1ms.
# See: https://dougblack.io/words/zsh-vi-mode.html
KEYTIMEOUT=1

zle -N zle-line-init
zle -N zle-keymap-select

# Backward deletion keybinds in insert mode.
bindkey '^?' backward-delete-char
bindkey '^W' backward-kill-word
bindkey '^H' backward-delete-char
bindkey '^U' backward-kill-line

# }}}

# Zsh: Temp {{{

if [[ -z $TMPDIR ]]; then
  export TMPDIR='/var/tmp/'
fi

if [[ ! -d $TMPDIR ]]; then
  mkdir -p -m 700 $TMPDIR
fi

# }}}

# Widget: Foreground/Background Vim {{{

# Stolen from Reddit, this is the Zsh equivalent of:
#
#   stty susp undef
#   bind '"\C-z":"fg\015"'
#
# Set ctrl + z to fg vim. It makes a great, efficient toggle on slow systems.

function _fg_vim {
  fg $EDITOR 2>/dev/null
}

stty susp undef
zle -N _fg_vim
bindkey '^z' _fg_vim

# }}}

# Lazyload: Angular CLI {{{
# See: https://peterlyons.com/problog/2018/01/zsh-lazy-loading/

function ng {
  unfunction "$0"
  # Load Angular CLI autocompletion.
  source <(ng completion script)
  ng "$@"
}

# }}}

# Aliases {{{

alias agu='sudo apt-get update && sudo apt-get -y dist-upgrade'
alias tmux='tmux -2' # "Force tmux to assume the terminal supports 256 colours."
alias tree='tree --gitignore'
alias tt='task n'
alias vi='nvim'
alias vim='nvim'
alias vimdiff='nvim -d'
alias b='br'
alias c='tty-clock -S -c -b -s'

# INFO(mark.grealish 2025-10-30): I haven't used taskwarrior in a while. Keep?
# alias t='task'
# alias ta='task add'

hash -d zdir=$ZSH_DIR
hash -d dotfiles=$DOTFILES_DIR
hash -d config="$XDG_CONFIG_HOME/dotfiles"
hash -d passwords="${HOME}/.password-store"

# }}}

# Other Scripts {{{

for plugin in $ZSH_DIR/plugins/*.zsh; do
  source $plugin
done

# }}}

# ZSH: Plugins {{{

# See: https://github.com/unixorn/awesome-zsh-plugins
# plugins=(git history-substring-search)
autoload -Uz compinit && compinit -C

# }}}

# Zsh: Local/Private Configuration {{{

for script in $XDG_CONFIG_HOME/dotfiles/*zsh; do
  source $script
done

# FIXME(mark 2025-10-26): Keeps breaking at home!
source ~/.config/broot/launcher/bash/br

# }}}

# zprof
